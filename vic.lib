biscale(mn, mx, in) = mn + (0.5 * (in + 1)) * (mx - mn);

scale(mn, mx, in) = mn + (in * (mx - mn));

breathylead(freq, amt) = no.noise * amp : 
fi.lowpass(4, 1000) :
//seq(i, 2, fi.resonbp(ba.midikey2hz(freq), Q, 0.9)) :> _ * 0.3 :
fi.resonhp(cutoff * jitter(cutoff), 10, 0.9) 
with {
	amp = 0.3;
	Q = scale(1, 3, amt);
	cutoff = scale(500, 4000, 1 - amt);
	jitter(f) = biscale(0.9, 1.0, os.osc(f * 0.01));
};
 
lead_sound(nn, amt) = sawlead(nn) *0.9 + lscale(0.1, amt) * breathylead(nn, amt)  : _ * 0.5;

sawlead(freq) = 
os.sawtooth(ba.midikey2hz(freq + (vib * vib_depth))) * 0.3 : ve.moog_vcf(0.4, 1000) 
with {
	vib_depth = biscale(0.01, 0.2, os.osc(1/12));
	vib_rate = biscale(4, 6, os.osc(1/11));
	vib = os.osc(vib_rate);
};

fosc(nn, mod, car, idx) = 
	os.osc(car*freq + mod*freq*idx*os.osc(freq*mod))
with {
	freq = ba.midikey2hz(nn);
};

lfo(freq) = biscale(0, 1, os.osc(freq));

chords(amt) = 
fosc(60,1,1,3 * amt) * lfo(1/10) * scale(0.1, 1, lscale(0.1, amt)) + 
fosc(59,1,0.999,1) * lfo(1/12)  * lscale(0.2, amt) + 
fosc(55,1,1.001,1) * lfo(1/9) * scale(0.1, 1, lscale(0.3, amt)) + 
fosc(62,2,1,1 * amt) * lfo(1/13) * lscale(0.4, amt) 
: _ * 0.1;

lscale(thresh, amt) = ((amt - thresh) / (1.0 - thresh)) * (amt > thresh);

bass(amt) = 
fosc(36, 1, 1.001, 8 * amt) * scale(amt, 1, lfo(1/11)) + 
fosc(24, 1, 0.999, 8 * amt) * scale(amt, 1, lfo(1/11)) * 0.5 : _ * 0.2 * lscale(0.7, amt); 

noise_sources = no.multinoise(5);

rnd(n) = noise_sources : ba.selectn(5, n);

trand(rnd, t) = rnd : ba.sAndH(t);

maytrig(thresh, rnd, t) = trand(rnd, t) : (_ > thresh) * t ;

pluckish(tempo, nn, ns) = 
ba.beat(tempo * 2) : maytrig(0.9, ns) : ba.toggle : en.ar(0.001, 0.04) : _ * fosc(nn, 1, 1, 1);

plucks = 
pluckish(60, 64, rnd(0)) +
pluckish(120, 67, rnd(1)) +
pluckish(110, 71, rnd(2))

: _ * 0.6 : echo ;

echo = +~(de.fdelay(4 * ma.SR, 2 * ma.SR) * 0.8);


noise_ticks(amt2) = clk : ba.toggle : 
en.ar(0.001, 0.001) * os.saw2(trand(rnd(3), clk) * 3000 + 100) * 0.5 *amt2  
//fi.resonbp(trand(rnd(3), clk) * 3000 + 1000, 1, 1.0)
with {
clk = ba.beat(scale(180, 480, amt2));
};



global_fx(amt, attn, dry) =  dry + chords(amt) + bass(amt) + plucks * lscale(0.3, amt) + noise_ticks(attn) <:_,_ : 


re.zita_rev1_stereo(10, 350, 4000, 10, 20, 96000) : ba.selectn(2, 0) : (1 - amt) * dry + amt *  _;

